plugins {
    id 'org.quiltmc.loom' version '1.+'
    id 'maven-publish'
    id 'idea'
    id 'com.modrinth.minotaur' version '2.+'
    id 'io.github.CDAGaming.cursegradle' version '1.6.1'
}

archivesBaseName = "${mod_id}-quilt-${minecraft_version}"

repositories {
    maven {
        name = 'TarraformersMC'
        url = 'https://maven.terraformersmc.com/releases'
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.layered {
        mappings "org.quiltmc:quilt-mappings:${quilt_mappings_version}:intermediary-v2"
        officialMojangMappings()
    }

    modImplementation "org.quiltmc:quilt-loader:${quilt_loader_version}"
    modApi "org.quiltmc.quilted-fabric-api:quilted-fabric-api:${quilt_version}"
    modApi "com.terraformersmc:modmenu:${modmenu_version}"

    implementation group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.1'

    implementation project(':Common')
}

loom {
    runs {
        client {
            client()
            setConfigName('Quilt Client')
            ideConfigGenerated(true)
            runDir('run')
        }
        server {
            server()
            setConfigName('Quilt Server')
            ideConfigGenerated(true)
            runDir('run')
        }
    }
}

processResources {
    from project(':Common').sourceSets.main.resources
    inputs.property 'version', project.version

    filesMatching('quilt.mod.json') {
        expand 'version': project.version
    }
}

tasks.withType(JavaCompile) {
    source(project(':Common').sourceSets.main.allSource)
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId project.archivesBaseName
            version project.version
            from components.java
        }
    }

    repositories {
        if (System.getenv("DIONTRYBAN_MAVEN_TOKEN") != null) {
            maven {
                url = 'https://maven.diontryban.com/releases'
                credentials {
                    username = 'trikzon'
                    password = System.getenv("DIONTRYBAN_MAVEN_TOKEN")
                }
            }
        }
    }
}

modrinth {
    token = System.getenv("MODRINTH_TOKEN")
    projectId = 'Q8xUICr6'
    versionNumber = "${project.version}-quilt"
    versionName = "[Quilt ${project.minecraft_version}] v${project.version}"
    versionType = 'release'
    changelog = rootProject.file('MINI_CHANGELOG.md').text
    uploadFile = remapJar
    gameVersions = ['1.19.4']
    loaders = ['quilt']
    dependencies {
        required.project 'fabric-api'
        optional.project 'modmenu'
    }
}

curseforge {
    apiKey = System.getenv('CURSEFORGE_TOKEN')
    project {
        id = '838498'
        releaseType = 'release'
        changelog = rootProject.file('MINI_CHANGELOG.md').text
        changelogType = 'markdown'
        mainArtifact(remapJar) {
            it.displayName = "[Quilt ${project.minecraft_version}] v${project.version}"
        }
        addGameVersion 'Quilt'
        addGameVersion 'Client'
        addGameVersion 'Server'
        addGameVersion 'Java 17'
        addGameVersion '1.19.4'
        relations {
            it.requiredDependency 'qsl'
            it.optionalDependency 'modmenu'
        }
    }
}
